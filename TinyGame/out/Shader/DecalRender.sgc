#include "Common.sgc"
#include "ScreenVertexShader.sgc"

#if PIXEL_SHADER

#if USE_MATERIAL
#define MATERIAL_TEXCOORD_NUM 1
#include "MaterialProcess.sgc"
#include "DeferredShadingCommon.sgc"

DEFINE_TEXTURE(Texture2D, NormalTexture);
#endif

DEFINE_TEXTURE(Texture2D, WorldPositionTexture);
uniform float4x4  DecalTransform;

PS_ENTRY_START(MainPS)
	PS_INPUT_STRUCT(VSOutputParameters VSOutput, 0)
	PS_OUTPUT(float4 OutColor, 0)
PS_ENTRY_END(MainPS)
{
	float3 worldPos = TEXTURE_SAMPLE(WorldPositionTexture, VSOutput.UVs).rgb;
	float  sceneDepth = TEXTURE_SAMPLE(WorldPositionTexture, VSOutput.UVs).a;
	float3 localPos = float3(mul(DecalTransform, float4( worldPos , 1 ) ));

	if ( sceneDepth == 0.0 || any(localPos < 0.0) || any(localPos > 1.0))
		discard;

	float2 DecalUV = localPos.xy;
#if USE_MATERIAL

	MaterialParametersPS materialParameters;
	materialParameters.worldPos = worldPos;
	materialParameters.worldNormal = DecodeNormal(TEXTURE_SAMPLE(NormalTexture, VSOutput.UVs).rgb);
	materialParameters.texCoords[0] = DecalUV;

	MaterialOutputPS materialOutput = InitMaterialOutputPS();
	Material_CalcParametersPS(materialOutput, materialParameters);
	OutColor = float4( materialOutput.emissiveColor , materialOutput.opacity );

#else
	OutColor = float4(DecalUV, 0 , 1);
#endif
}

#endif